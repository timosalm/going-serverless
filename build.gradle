plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5' // SB 3.3 Adds support to unpack uber JARs to a CDS friendly layout
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.graalvm.buildtools.native' version '0.10.3' apply false
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}


repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

def buildProfile = project.hasProperty('buildProfile') ? project.getProperty('buildProfile') : 'default'
println "Build Profile: ${buildProfile}"

if (buildProfile == 'graalvm') {
	apply plugin: 'org.graalvm.buildtools.native'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	if (buildProfile == 'crac') {
		implementation 'org.crac:crac'
	}
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

if (buildProfile == 'graalvm') {
	tasks.named('jar') {
		enabled = false // disable plain archive, otherwise native image build wild fail with "Error: /workspace is a directory. (-jar requires a valid jarfile)"
	}
}

tasks.named("bootBuildImage") {
	environment["BP_JVM_VERSION"] = "23"
	if (buildProfile == 'cds') {
		environment["BP_JVM_CDS_ENABLED"] = "true"
		environment["BP_SPRING_AOT_ENABLED"] = "true"
	}
}